buildscript {
    ext {
        springBootVersion = '1.5.8.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")

    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.6"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: "io.spring.dependency-management"
apply plugin: 'maven-publish'

group = 'com.mimacom.demo.customers'
version = '1.0.0-RELEASE'
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

ext['spring-restdocs.version'] = '2.0.0.RELEASE'


ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('com.h2database:h2:1.4.196')
    asciidoctor ('org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.0.RELEASE')
    testCompile ('org.springframework.restdocs:spring-restdocs-mockmvc:2.0.0.RELEASE')
    runtime('org.springframework.boot:spring-boot-devtools')
    testCompile('org.springframework.boot:spring-boot-starter-test')

}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}
